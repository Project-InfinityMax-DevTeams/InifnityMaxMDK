plugins {
    id 'base'
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.9.25' apply false
}

group = 'com.yuyuto'
version = '1.0.0'

def prop = { String name, def defaultVal = null ->
    project.hasProperty(name) ? project.property(name) : defaultVal
}

def javaVer = prop('java_version', '17')
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(Integer.parseInt(javaVer.toString()))
    }
}

// Subprojects共通設定
subprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group = rootProject.group
    version = rootProject.version

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(Integer.parseInt(prop('java_version', '17').toString()))
        }
        withSourcesJar()
    }

    kotlin {
        jvmToolchain(Integer.parseInt(prop('java_version', '17').toString()))
    }

    repositories {
        mavenCentral()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Project-InfinityMax-DevTeams/InfinityMaxAPI")
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GPR_TOKEN")
            }
        }
        maven { url 'https://maven.fabricmc.net/' }
        maven { url 'https://maven.minecraftforge.net/' }
    }

    dependencies {
        implementation 'org.joml:joml:1.10.5'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = Integer.parseInt(prop('java_version', '17').toString())
    }
}

// MOD Artifacts集約
def artifactOutputDir = layout.buildDirectory.dir("artifacts")

tasks.register("cleanModArtifacts", Delete) {
    group = "build"
    description = "Deletes generated Forge/Fabric mod artifacts from the root build directory."
    delete artifactOutputDir
}

tasks.register("assembleModArtifacts") {
    group = "build"
    description = "Builds Forge and Fabric distributable jars and gathers them into build/artifacts/."

    dependsOn(
            ":forge:jar",
            ":fabric:remapJar"
    )

    doLast {
        def outputDir = artifactOutputDir.get().asFile
        outputDir.mkdirs()

        copy {
            from project(":forge").tasks.named("jar").get().archiveFile
            into outputDir
        }
        copy {
            from project(":fabric").tasks.named("remapJar").get().archiveFile
            into outputDir
        }

        logger.lifecycle("Mod artifacts assembled at: ${outputDir}")
    }
}

tasks.named("clean") { 
    dependsOn("cleanModArtifacts") 
}

tasks.named("build") {  
    dependsOn("assembleModArtifacts") 
}